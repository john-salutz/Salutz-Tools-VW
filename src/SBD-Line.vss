{//////////////////////////////////////////////////////////////////////////////
//  Developed by John Salutz, (c) 2020. Please share freely.
//
//  SBD Line draws a dynamic polyline with midpoint control & markers.
//////////////////////////////////////////////////////////////////////////////}

{//////////////////////////////////////////////////////////////////////////////
//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the GNU General Public License as published by
//  the Free Software Foundation, either version 3 of the License, or
//  (at your option) any later version.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//  GNU General Public License for more details.
//  <https://www.gnu.org/licenses/>
//////////////////////////////////////////////////////////////////////////////}

PROCEDURE SBD_Line;

CONST
	RGBmax = 65535; {Max value for setting RGB colors in VS fuctions}

VAR
	{Standard variables}
	s_ObjName                                               : STRING;
	h_Obj,h_ObjRecord,h_ObjWall                             : HANDLE;
	b_Result                                                : BOOLEAN;
	i_Counter                                               : INTEGER;
	{Script specific variables}
	pt1,pt2                                                 : POINT;
	p_Start,p_StartCorner,p_Midpoint,p_EndCorner,p_End      : POINT;
	h_Poly                                                  : HANDLE;
	r_CornerSize,r_test                                     : REAL;
	i_CornerStyle,i_FlipX1,i_FlipX2                         : INTEGER;

{===== LOCAL FUNCTION DECLARATIONS =====}

{// Sets Version info parameter //}
PROCEDURE Set_Version_Info(s : STRING);
BEGIN
	SetRField(h_Obj,s_ObjName,'ver',s);
END;


{// Sets the specified text object to use the attributes of PIO //}
PROCEDURE Use_PIO_Text_Attributes (h : HANDLE);
BEGIN
	SetObjectVariableBoolean(h,800,True);
END;

{// Sets Background Fore and Back as White to Black, percentage set by 'r',
format 0=White, 0.1 thru 0.9=10% to 90%, 1=Black //}
PROCEDURE Set_Background_WtoB (h : HANDLE; r : REAL);
VAR i : LONGINT;
BEGIN
	IF r<0 THEN r:=0;
	IF r>1 THEN r:=1;
	i := Trunc(RGBmax*(1-r));
	SetFillBack(h, i, i, i);
	SetFillFore(h, i, i, i);
	SetFPat(h, 1); {Blank pattern}
END;

{// Sets Pen Fore and Back as White to Black, percentage set by 'r',
format 0=White, 0.1 thru 0.9=10% to 90%, 1=Black //}
PROCEDURE Set_Pen_WtoB (h : HANDLE; r : REAL);
VAR i : LONGINT;
BEGIN
	IF r<0 THEN r:=0;
	IF r>1 THEN r:=1;
	i := Trunc(RGBmax*(1-r));
	SetPenFore(h, i, i, i);
	SetPenBack(h, i, i, i);
	SetLSN(h,2); {Solid pattern}
END;

FUNCTION Find_Layer (obj : HANDLE) : HANDLE;
BEGIN
	IF GetLayer(obj) = NIL THEN ResetObject(obj)
	ELSE Find_Layer := GetLayer(obj);
END;

PROCEDURE Resolve_MidPoint_Conflict;
VAR
	matchPoint,matchField : BOOLEAN;
	correctValue          : STRING;
BEGIN
	matchPoint := (P__STOREMIDPOINTDIST = Num2StrF(PCONTROLPOINT02X));
	matchField := (P__STOREMIDPOINTDIST = Num2StrF(PMIDPOINTDIST));
	correctValue := '';
	IF matchPoint AND NOT matchField THEN BEGIN
		{Field correct, set value from field}
		correctValue := Num2StrF(PMIDPOINTDIST);
	END ELSE IF NOT matchPoint AND matchField THEN BEGIN
		{Point correct, set value from control point}
		correctValue := Num2StrF(PCONTROLPOINT02X);
	END ELSE IF NOT matchPoint AND NOT matchField THEN BEGIN
		{Neither correct, set value from control point}
		correctValue := Num2StrF(PCONTROLPOINT02X);
	END;
	IF correctValue<>'' THEN BEGIN
		SetRField(h_Obj,s_ObjName,'ControlPoint02X',correctValue);
		SetRField(h_Obj,s_ObjName,'MidpointDist',correctValue);
		SetRField(h_Obj,s_ObjName,'__StoreMidpointDist',correctValue);
		ResetObject(h_Obj);
	END;
END; {PROCEDURE Resolve_MidPoint_Conflict}

PROCEDURE Resolve_Endpoint_Conflict;
VAR
	matchPoint,matchField : BOOLEAN;
	correctValue          : STRING;
BEGIN
	matchPoint := (P__STOREENDDIST = Num2StrF(PCONTROLPOINT03X));
	matchField := (P__STOREENDDIST = Num2StrF(PENDDIST));
	correctValue := '';
	IF matchPoint AND NOT matchField THEN BEGIN
		{Field correct, set value from field}
		correctValue := Num2StrF(PENDDIST);
	END ELSE IF NOT matchPoint AND matchField THEN BEGIN
		{Point correct, set value from control point}
		correctValue := Num2StrF(PCONTROLPOINT03X);
	END ELSE IF NOT matchPoint AND NOT matchField THEN BEGIN
		{Neither correct, set value from control point}
		correctValue := Num2StrF(PCONTROLPOINT03X);
	END;
	IF correctValue<>'' THEN BEGIN
		SetRField(h_Obj,s_ObjName,'ControlPoint03X',correctValue);
		SetRField(h_Obj,s_ObjName,'EndDist',correctValue);
		SetRField(h_Obj,s_ObjName,'__StoreEndDist',correctValue);
		ResetObject(h_Obj);
	END;
END; {PROCEDURE Resolve_Endpoint_Conflict}

FUNCTION Resolve_Begin_Conflict : BOOLEAN;
BEGIN
	IF (PCONTROLPOINT01X <> 0) OR (PCONTROLPOINT01Y <> 0) THEN BEGIN
		HMove(h_Obj,PCONTROLPOINT01X,PCONTROLPOINT01Y);
		SetRField(h_Obj,s_ObjName,'ControlPoint01X','0');
		SetRField(h_Obj,s_ObjName,'ControlPoint01Y','0');
		SetRField(
				h_Obj,s_ObjName,'ControlPoint02X',
				Num2StrF(PCONTROLPOINT02X-PCONTROLPOINT01X)
				);
		SetRField(h_Obj,s_ObjName,'ControlPoint02Y',
				Num2StrF(PCONTROLPOINT02Y-PCONTROLPOINT01Y)
				);
		SetRField(
				h_Obj,s_ObjName,'ControlPoint03X',
				Num2StrF(PCONTROLPOINT03X-PCONTROLPOINT01X)
				);
		SetRField(h_Obj,s_ObjName,'ControlPoint03Y',
				Num2StrF(PCONTROLPOINT03Y-PCONTROLPOINT01Y)
				);
		ResetObject(h_Obj);
		Resolve_Begin_Conflict := TRUE;
	END ELSE Resolve_Begin_Conflict := FALSE;
END;

{====================================}
{------------- < MAIN > -------------}
{====================================}

{// Retrieve plug-in object information //}
BEGIN IF GetCustomObjectInfo(s_ObjName,h_Obj,h_ObjRecord,h_ObjWall) THEN BEGIN
	Set_Version_Info('0 . 4');
	Use_PIO_Text_Attributes(h_Obj);
	SetParameterVisibility(h_Obj,'ControlPoint01X',FALSE);
	SetParameterVisibility(h_Obj,'ControlPoint01Y',FALSE);
	SetParameterVisibility(h_Obj,'ControlPoint02X',FALSE);
	SetParameterVisibility(h_Obj,'ControlPoint02Y',FALSE);
	SetParameterVisibility(h_Obj,'ControlPoint03X',FALSE);
	SetParameterVisibility(h_Obj,'ControlPoint03Y',FALSE);

	{===== Parameter checking =====}
	IF NOT Resolve_Begin_Conflict THEN BEGIN
		Resolve_MidPoint_Conflict;
		Resolve_Endpoint_Conflict;
	END;
	ResetObject(h_Obj);

	{===== Variable Assignment =====}
	p_Start.X := 0;
	p_Start.Y := 0;
	p_Midpoint.X := PCONTROLPOINT02X;
	p_Midpoint.Y := PCONTROLPOINT02Y;
	p_End.X := PCONTROLPOINT03X;
	p_End.Y := PCONTROLPOINT03Y;
	IF PCORNERS='Square' THEN BEGIN
		i_CornerStyle := 0;
		SetParameterVisibility(h_Obj,'RoundRadius',FALSE);
	END;
	IF PCORNERS='Rounded' THEN BEGIN
		i_CornerStyle := 3;
		SetParameterVisibility(h_Obj,'RoundRadius',TRUE);
	END;

	{// Corner Sizing //}
	r_CornerSize := PROUNDRADIUS;
	IF Abs(p_End.Y)<(r_CornerSize*2)
		THEN r_CornerSize := Abs(p_End.Y/2);
	IF Abs(p_Midpoint.X)<(r_CornerSize)
		THEN r_CornerSize := Abs(p_Midpoint.X/2);
	IF Abs(p_End.X-p_Midpoint.X)<(r_CornerSize)
		THEN r_CornerSize := Abs((p_End.X-p_Midpoint.X)/2);
	
	IF p_End.Y<p_Start.Y THEN r_CornerSize := -r_CornerSize;

	{===== Draw Poly =====}
	MoveTo(0,0);
	BeginPoly;
	{Start point}
	Add2DVertex(p_Start.X,p_Start.Y,0,0);
	IF PCORNERS='Angle' THEN BEGIN
		{// Set Direction Flips //}
		IF (p_Midpoint.X<p_Start.X) THEN i_FlipX1 := -1 ELSE i_FlipX1 := 1;
		IF (p_End.X<p_Midpoint.X) THEN i_FlipX2 := -1 ELSE i_FlipX2 := 1;
		{Pre offset}
		Add2DVertex(p_Midpoint.X-(Abs(r_CornerSize)*i_FlipX1),p_Start.Y,0,0);
		{Post offset}
		Add2DVertex(p_Midpoint.X,p_Start.Y+r_CornerSize,0,0);
		{Pre offset}
		Add2DVertex(p_Midpoint.X,p_End.Y-r_CornerSize,0,0);
		{Post offset}
		Add2DVertex(p_Midpoint.X+(Abs(r_CornerSize)*i_FlipX2),p_End.Y,0,0);
	END ELSE BEGIN
		{Corner1}
		Add2DVertex(p_Midpoint.X,p_Start.Y,i_CornerStyle,Abs(r_CornerSize));
		{Corner2}
		Add2DVertex(p_Midpoint.X,p_End.Y,i_CornerStyle,Abs(r_CornerSize));
	END;
	{End point}
	Add2DVertex(p_End.X,p_End.Y,0,0);
	EndPoly;
	h_Poly := LNewObj;

	{===== Set poly markers =====}
	IF PUSEBEGINMARK THEN
	b_Result := SetObjBeginningMarker(
			h_Poly, 2, 0, 1/GetLScale(Find_Layer(h_Obj)),
			1/GetLScale(Find_Layer(h_Obj)), 0, 0, TRUE
			);

	IF PUSEENDMARK THEN
	b_Result := SetObjEndMarker(
			h_Poly, 2176, 15, 3/GetLScale(Find_Layer(h_Obj)),
			2/GetLScale(Find_Layer(h_Obj)), 0, 0, TRUE
			);
	SetFPat(h_Poly,0);
	SetRField(
			h_Obj, s_ObjName, 'ControlPoint02Y',
			Num2StrF((p_End.Y-p_Start.Y)/2)
			);

END; END; Run(SBD_Line);
